buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
    id "com.github.kt3k.coveralls" version "2.5.0"
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

// Primary configuration
group = 'com.mariten'
version = '1.2.1'
description = 'Utilities for handling Japanese kana text easily in Java'

// JDK version compatibility
sourceCompatibility = 1.6
targetCompatibility = 1.6

dependencies {
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'junit:junit:4.11'
}

test {
    systemProperty 'test_with_php', "$System.env.TEST_WITH_PHP"
}

jacoco {
    toolVersion = '0.7.5.201505241946'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

// Custom tasks for creating source/javadoc JARs
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "mariten"
            name "Jeff Case"
            email "jeff@mariten.com"
        }
    }
    scm {
        connection "scm:git:git://github.com/mariten/kanatools-java.git"
        developerConnection "scm:git:git://github.com/mariten/kanatools-java.git"
        url "https://github.com/mariten/kanatools-java"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Kana Tools for Java')
                root.appendNode('description', project.getDescription())
                root.appendNode('url', 'http://mariten.github.io/kanatools-java')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = 'mariten'
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = project.getName()
        desc = project.getDescription()
        licenses = ['MIT']

        websiteUrl = 'http://mariten.github.io/kanatools-java'
        issueTrackerUrl = 'https://github.com/mariten/kanatools-java/issues'
        vcsUrl = 'https://github.com/mariten/kanatools-java.git'

        githubRepo = 'mariten/kanatools-java'
        githubReleaseNotesFile = 'HISTORY.md'

        publicDownloadNumbers = true
        labels = ['japanese', 'conversion', 'kana', 'hiragana', 'katakana', 'romaji', 'zenkaku', 'hankaku', '日本語', 'かな変換', 'ひらがな', 'カタカナ', '仮名', 'ローマ字', '全角', '半角']

        version {
            name = project.getVersion()
            vcsTag = project.getVersion()
            released  = new Date()
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
